import org.apache.tools.ant.filters.ReplaceTokens


buildscript {
    ext {
        kotlin_version = '1.2.71'
        coroutines_version = '0.25.3'
        spigot_api_version = '1.13.1-R0.1-SNAPSHOT'
        protcollib_version = '4.4.0-SNAPSHOT'
        slf4j_version = '1.7.25'
        exposed_version = '0.10.4'
        rxjava_version = '2.2.2'
        rxkotlin_version = '2.3.0'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}



group = 'click.seichi'
version = '1.0-SNAPSHOT'

apply plugin: 'kotlin'

sourceCompatibility = targetCompatibility = 1.8
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

processResources {
    filteringCharset = 'UTF-8'
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [version: project.version])
        filter(ReplaceTokens, tokens: [name: project.rootProject.name])
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        url = 'https://jitpack.io'
    }
    maven {
        name = 'exposed-repo'
        url = 'https://dl.bintray.com/kotlin/exposed/'
    }
    // ProtocolLib
    maven {
        name = 'dmulloy2-repo'
        url = 'http://repo.dmulloy2.net/nexus/repository/public/'
    }
    // Multiverse-Core
    maven {
        name = 'elmaker-repo'
        url = 'http://maven.elmakers.com/repository'
    }
    // Multiverse-Core
    maven {
        name = 'onarandombox-repo'
        url = 'http://repo.onarandombox.com/content/groups/public'
    }
    // perWorldInventory
    maven {
        name = 'codemc-releases'
        url = 'https://repo.codemc.org/repository/maven-releases'
    }
    // perWorldInventory
    maven {
        name = 'aikar'
        url = 'https://repo.aikar.co/content/groups/aikar/'
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
    compile group: 'org.spigotmc', name: 'spigot-api', version: spigot_api_version
    compile group: 'org.spigotmc', name: 'spigot', version: spigot_api_version

    embed group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version

    compile group: 'com.comphenix.protocol', name: 'ProtocolLib', version: protcollib_version
    compile group: 'com.onarandombox.multiversecore', name: 'Multiverse-Core', version: '2.6.0'

    embed group: 'co.aikar', name: 'acf-paper', version: '0.5.0-SNAPSHOT'
    compile group: 'me.ebonjaeger', name: 'perworldinventory-kt', version: '2.1.0'

    embed group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: coroutines_version
    embed group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-rx2', version: coroutines_version
    embed group: "org.slf4j", name: "slf4j-log4j12", version: slf4j_version
    embed group: "org.jetbrains.exposed", name: "exposed", version: exposed_version
    embed group: "io.reactivex.rxjava2", name: "rxjava", version: rxjava_version
    embed group: "io.reactivex.rxjava2", name: "rxkotlin", version: rxkotlin_version

    compile fileTree(dir: "libs", include: "*.jar")
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}

kotlin { experimental { coroutines 'enable' } }